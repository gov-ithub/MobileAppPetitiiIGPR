buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "io.realm:realm-gradle-plugin:$rootProject.ext.realm"
        classpath "me.tatarka:gradle-retrolambda:$rootProject.ext.retrolambdaPlugin"
        classpath "com.google.gms:google-services:$rootProject.ext.googleServicesPlugin"
        classpath "com.github.andreiverdes:license-gradle-plugin:master-SNAPSHOT"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.github.hierynomus.license'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "ro.politiaromana.petitie.mobile.android"
        minSdkVersion 16
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "0.6"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        alpha {
            applicationIdSuffix '.alpha'
            versionNameSuffix '-ALPHA'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
//        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

task copyLicensesFile(type: Copy) {
    description = 'Copying the licenses assets file...'
    from 'build/reports/license'
    include 'dependency-license.json'
    into 'src/main/assets'
}

downloadLicenses {
    ext.apacheTwo = license('Apache 2.0', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
    licenses = [
            (group('com.google.firebase')) : apacheTwo,
            (group('com.google.android.gms')) : apacheTwo,
            (group('com.android.support')) : apacheTwo,
            (group('io.realm')) : apacheTwo
    ]
    report.xml.enabled = false
    report.html.enabled = false
    report.json.enabled = true
    reportByDependency = true
    reportByLicenseType = false
    includeProjectDependencies = true
    dependencyConfiguration = 'compile'
}

project.afterEvaluate {
    generateDebugResources.dependsOn tasks['copyLicensesFile']
    generateReleaseResources.dependsOn tasks['copyLicensesFile']
    copyLicensesFile.dependsOn tasks['downloadLicenses']
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:multidex:1.0.1'

    retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:$rootProject.ext.retrolambda"

    compile "com.android.support:multidex:$rootProject.ext.multidex"
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibrary"
    compile "com.android.support:support-v4:$rootProject.ext.supportLibrary"
    compile "com.android.support:preference-v14:$rootProject.supportLibrary"
    compile "com.android.support:design:$rootProject.ext.supportLibrary"
    compile "com.google.firebase:firebase-core:$rootProject.ext.play"
    compile "com.google.firebase:firebase-crash:$rootProject.ext.play"
    compile "com.google.android.gms:play-services-places:$rootProject.ext.play"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:converter-jackson:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.ext.retrofit"
    compile "com.facebook.stetho:stetho:$rootProject.ext.stetho"
    compile "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stetho"
    compile "com.fasterxml.jackson.core:jackson-core:$rootProject.ext.jackson"
    compile "com.fasterxml.jackson.core:jackson-databind:$rootProject.ext.jackson"
    compile "com.fasterxml.jackson.core:jackson-annotations:$rootProject.ext.jackson"
    compile "io.reactivex:rxjava:$rootProject.ext.rxJava"
    compile "io.reactivex:rxandroid:$rootProject.ext.rxAndroid"
    compile "com.jakewharton.rxbinding:rxbinding:$rootProject.ext.rxBinding"
    compile "com.jakewharton.timber:timber:$rootProject.ext.timber"
    compile("com.mikepenz:materialdrawer:$rootProject.ext.materialDrawer") { transitive = true }
    compile "com.googlecode.libphonenumber:libphonenumber:$rootProject.ext.libPhoneNumber"
    compile "com.mikepenz:google-material-typeface:$rootProject.ext.materialTypefaces"
    compile "com.github.bumptech.glide:glide:$rootProject.ext.glide"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

// Uncomment once we have a Firebase Project on the web console
apply plugin: 'com.google.gms.google-services'
